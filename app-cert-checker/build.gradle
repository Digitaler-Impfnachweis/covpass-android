android {
    defaultConfig {
        applicationId "com.ibm.health.sampleapp"
        archivesBaseName = "hc-$version"
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "ega_fileprovider_authority", variant.applicationId
    }

    flavorDimensions "scope"
    productFlavors {
        egaOnly {
            dimension "scope"
        }
        epaOnly {
            dimension "scope"

            applicationIdSuffix ".epa"
            versionNameSuffix "-ePA"
        }
        healthConnect {
            dimension "scope"

            applicationIdSuffix ".healthconnect"
            versionNameSuffix "-HealthConnect"
        }
    }
}

dependencies {
    egaOnlyImplementation project(':ibmhealth-container-ega')
    egaOnlyImplementation project(':ibmhealth-ega-ui')
    egaOnlyImplementation libraries.egaDataTransfer

    epaOnlyImplementation project(':ibmhealth-container-epa')

    // Only needed for overriding AuthenticationData for internal testing
    epaOnlyImplementation project(':ibmhealth-epa-ui')
    epaOnlyImplementation(libraries.epaApi) {
        exclude group: 'org.threeten'
    }
    epaOnlyImplementation(libraries.epaDummyData) {
        exclude group: 'org.threeten'
    }

    healthConnectImplementation project(':ibmhealth-container-combined')
    healthConnectImplementation project(':ibmhealth-ega-ui')
    healthConnectImplementation libraries.egaDataTransfer

    // Only needed for overriding AuthenticationData for internal testing
    healthConnectImplementation project(':ibmhealth-epa-ui')
    healthConnectImplementation(libraries.epaApi) {
        exclude group: 'org.threeten'
    }
    healthConnectImplementation(libraries.epaDummyData) {
        exclude group: 'org.threeten'
    }

    implementation project(':ibmhealth-contract')
    implementation project(':ibmhealth-ui')

    // Common modules
    implementation libraries.commonHttp
    implementation libraries.commonSecurityAndroid
    implementation libraries.commonSecurityProvider

    implementation libraries.constraintLayout
    implementation libraries.rxJava
    implementation libraries.rxAndroid
    implementation libraries.rxKotlin
}

task lintDebug {
    dependsOn 'lintEgaOnlyDebug'
    dependsOn 'lintEpaOnlyDebug'
    dependsOn 'lintHealthConnectDebug'
}

task testDebugUnitTest {
    dependsOn 'testEgaOnlyDebugUnitTest'
    dependsOn 'testEpaOnlyDebugUnitTest'
    dependsOn 'testHealthConnectDebugUnitTest'
}
