buildscript {
    ext.kotlin_version = '1.4.31'
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.14.1'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.10.1'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.11.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'pl.allegro.tech.build.axion-release'

apply from: "$rootDir/gradle/common/ibmconfig.gradle"
setupRepos()
apply from: "$rootDir/gradle/common/dependency-updates.gradle"

apply from: "$rootDir/gradle/common/scm-version.gradle"
setupScmVersion()
group = 'com.ibm.health.common'
subprojects {
    def isLibraryProject = project.name != 'app' && !project.name.startsWith('app-')

    group = isLibraryProject ? rootProject.group : 'com.ibm.health.vaccination.app'
    version = rootProject.version

    def isAndroidProject = project.file('src/main/AndroidManifest.xml').exists()
    def isPlatformProject = project.name.endsWith('-bom')
    def isTestProject = project.name.endsWith('-test') || project.name == 'test'

    apply from: "$rootDir/dependencies.gradle"

    if (!isPlatformProject) {
        if (!isLibraryProject) {
            apply from: "$rootDir/gradle/common/android-app.gradle"
            androidApp(minVersion: 23, obfuscation: Obfuscation.Full)
        } else if (isAndroidProject) {
            apply from: "$rootDir/gradle/common/android-library.gradle"
            androidLibrary(minVersion: 23, kotlinExplicitApiMode: true)
        } else {
            apply from: "$rootDir/gradle/common/kotlin-library.gradle"
            kotlinLibrary(kotlinExplicitApiMode: true)
        }

        apply from: "$rootDir/gradle/common/ktlint.gradle"
        apply from: "$rootDir/gradle/common/detekt.gradle"

        if (isAndroidProject) {
            android {
                if (!isLibraryProject) {
                    def codeName = project.name.substring("app-".length()).replaceAll("-", "")
                    defaultConfig {
                        applicationId "$group.$codeName"
                        archivesBaseName = "$codeName-$version"
                    }
                }

                // Resolve build conflicts for test modules
                if (isTestProject) {
                    packagingOptions {
                        pickFirst 'META-INF/AL2.0'
                        pickFirst 'META-INF/LGPL2.1'
                    }
                }
                viewBinding {
                    enabled true
                }
            }

            dependencies {
                baseAndroid()
                if (isTestProject) {
                    androidTestLib()
                }
            }
        } else {
            dependencies {
                base()
                if (isTestProject) {
                    baseTestLib()
                }
            }
        }

        task verifyAll(group: 'verification') {
            dependsOn 'ktlint'
            dependsOn 'detekt'
            if (isAndroidProject) {
                dependsOn 'lintDebug'
                dependsOn 'testDebugUnitTest'
            } else {
                dependsOn 'test'
            }
        }

        if (isLibraryProject) {
            apply from: "$rootDir/gradle/common/coverage.gradle"
            trackCoverage(isOnCI)
        }
    }

    // TODO:
//    if (isLibraryProject && project.name != "common-app") {
//        apply from: "$rootDir/gradle/common/publish.gradle"
//        def url = TODO
//        publish(
//            url: url,
//            username: username,
//            password: password,
//            isPlatformProject: isPlatformProject,
//        )
//    }
}
