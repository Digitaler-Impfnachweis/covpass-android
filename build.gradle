buildscript {
    ext.kotlin_version = '1.4.32'
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.14.1'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.10.1'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.12.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.jaredsburrows:gradle-license-plugin:0.8.90'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'pl.allegro.tech.build.axion-release'

apply from: "$rootDir/gradle/common/ibmconfig.gradle"
setupRepos()
apply from: "$rootDir/gradle/common/dependency-updates.gradle"

apply from: "$rootDir/gradle/common/scm-version.gradle"
setupScmVersion()
group = 'com.ibm.health.vaccination'
subprojects {
    group = rootProject.group
    version = rootProject.version

    def isLibraryProject = project.name != 'app' && !project.name.startsWith('app-')
    def isAndroidProject = project.file('src/main/AndroidManifest.xml').exists()
    def isPlatformProject = project.name.endsWith('-bom')
    def isTestProject = project.name.endsWith('-test') || project.name == 'test'

    apply from: "$rootDir/dependencies.gradle"

    if (!isPlatformProject) {
        if (!isLibraryProject) {
            apply from: "$rootDir/gradle/common/android-app.gradle"
            androidApp(minVersion: 23, obfuscation: Obfuscation.Full)
            apply plugin: 'com.jaredsburrows.license'

            licenseReport {
                generateCsvReport = false
                generateHtmlReport = true
                generateJsonReport = false
            }
        } else if (isAndroidProject) {
            apply from: "$rootDir/gradle/common/android-library.gradle"
            androidLibrary(minVersion: 23, kotlinExplicitApiMode: true)
        } else {
            apply from: "$rootDir/gradle/common/kotlin-library.gradle"
            kotlinLibrary(kotlinExplicitApiMode: true)
        }

        apply from: "$rootDir/gradle/common/ktlint.gradle"
        apply from: "$rootDir/gradle/common/detekt.gradle"

        if (isAndroidProject) {
            android {
                if (!isLibraryProject) {
                    def codeName = project.name.substring("app-".length()).replaceAll("-", "")
                    defaultConfig {
                        applicationId "$group.$codeName"
                        archivesBaseName = "$codeName-$version"
                    }
                }

                defaultConfig {
                    vectorDrawables.useSupportLibrary = true
                }

                buildFeatures {
                    viewBinding true
                }

                // Resolve build conflicts for test modules
                if (isTestProject) {
                    packagingOptions {
                        pickFirst 'META-INF/AL2.0'
                        pickFirst 'META-INF/LGPL2.1'
                    }

                    lintOptions {
                        abortOnError false
                    }
                }
            }

            dependencies {
                baseAndroid()
                if (isTestProject) {
                    androidTestLib()
                }
            }
        } else {
            dependencies {
                base()
                if (isTestProject) {
                    baseTestLib()
                }
            }
        }

        task verifyAll(group: 'verification') {
            dependsOn 'ktlint'
            dependsOn 'detekt'
            if (isAndroidProject) {
                dependsOn 'lintDebug'
                dependsOn 'testDebugUnitTest'
            } else {
                dependsOn 'test'
            }
        }

        if (isLibraryProject) {
            apply from: "$rootDir/gradle/common/coverage.gradle"
            trackCoverage(isOnCI)
        }
    }

    // TODO:
//    if (isLibraryProject && project.name != "common-app") {
//        apply from: "$rootDir/gradle/common/publish.gradle"
//        def url = TODO
//        publish(
//            url: url,
//            username: username,
//            password: password,
//            isPlatformProject: isPlatformProject,
//        )
//    }
}
